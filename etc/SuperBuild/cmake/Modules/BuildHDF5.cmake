include(ExternalProject)

set(BUILD_STATIC_LIBS OFF)
if(NOT BUILD_SHARED_LIBS)
    set(BUILD_STATIC_LIBS ON)
endif()

set(HDF5_ARGS
    -DCMAKE_MODULE_PATH=${CMAKE_MODULE_PATH}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
    -DCMAKE_PREFIX_PATH=${CMAKE_INSTALL_PREFIX}
    -DCMAKE_POLICY_DEFAULT_CMP0091:STRING=NEW
    -DCMAKE_FIND_FRAMEWORK=${CMAKE_FIND_FRAMEWORK}
    -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
    -DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}
    -DCMAKE_MSVC_RUNTIME_LIBRARY=${CMAKE_MSVC_RUNTIME_LIBRARY}
    -DBUILD_SHARED_LIBS=${BUILD_SHARED_LIBS}
    -DBUILD_STATIC_LIBS=${BUILD_STATIC_LIBS}
    -DHDF5_NO_PACKAGES=ON
    -DBUILD_TESTING=OFF
    -DHDF5_BUILD_TOOLS=OFF
    -DHDF5_BUILD_EXAMPLES=OFF)
if(CMAKE_CXX_STANDARD)
    set(HDF5_ARGS ${HDF5_ARGS} -DCMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD})
endif()

ExternalProject_Add(
    HDF5
    PREFIX ${CMAKE_CURRENT_BINARY_DIR}/HDF5
    DEPENDS ZLIB
    GIT_REPOSITORY https://github.com/HDFGroup/hdf5.git
    GIT_TAG hdf5-1_12_0
    CMAKE_ARGS ${HDF5_ARGS})
