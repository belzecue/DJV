include(ExternalProject)

set(OCIO_BUILD_SHARED ON)
set(OCIO_BUILD_STATIC OFF)
if(NOT BUILD_SHARED_LIBS)
    set(OCIO_BUILD_SHARED OFF)
    set(OCIO_BUILD_STATIC ON)
endif()

set(OCIO_ARGS
    -DCMAKE_MODULE_PATH=${CMAKE_MODULE_PATH}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
    -DCMAKE_INSTALL_LIBDIR=lib
    -DCMAKE_PREFIX_PATH=${CMAKE_INSTALL_PREFIX}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_POLICY_DEFAULT_CMP0091=NEW
    -DCMAKE_POLICY_DEFAULT_CMP0066=NEW
    -DCMAKE_POLICY_DEFAULT_CMP0056=NEW
    -DCMAKE_FIND_FRAMEWORK=${CMAKE_FIND_FRAMEWORK}
    -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
    -DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}
    -DCMAKE_MSVC_RUNTIME_LIBRARY=${CMAKE_MSVC_RUNTIME_LIBRARY}
    -DBUILD_SHARED_LIBS=${BUILD_SHARED_LIBS}
    -DOCIO_BUILD_SHARED=${OCIO_BUILD_SHARED}
    -DOCIO_BUILD_STATIC=${OCIO_BUILD_STATIC}
    -DOCIO_BUILD_APPS=OFF
    -DOCIO_BUILD_NUKE=OFF
    -DOCIO_BUILD_DOCS=OFF
    -DOCIO_BUILD_TESTS=OFF
    -DOCIO_BUILD_PYGLUE=OFF
    -DOCIO_STATIC_JNIGLUE=OFF
    -DOCIO_USE_BOOST_PTR=OFF
    -DYAML_CPP_OBJECT_LIB_EMBEDDED=ON
    -DTINYXML_OBJECT_LIB_EMBEDDED=ON)
if(CMAKE_CXX_STANDARD)
    set(OCIO_ARGS ${OCIO_ARGS} -DCMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD})
endif()
cmake_host_system_information(RESULT HAS_SSE2 QUERY HAS_SSE2)
set(OCIO_ARGS ${OCIO_ARGS} -DOCIO_USE_SSE=${HAS_SSE2})

file(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/OCIO/src/OCIO-build/ext)

ExternalProject_Add(
    OCIO
    PREFIX ${CMAKE_CURRENT_BINARY_DIR}/OCIO
    URL "http://github.com/imageworks/OpenColorIO/archive/v1.1.1.tar.gz"
    PATCH_COMMAND
        ${CMAKE_COMMAND} -E tar xf
        ${CMAKE_SOURCE_DIR}/OCIO-patch.tar.gz
    CMAKE_ARGS ${OCIO_ARGS})

